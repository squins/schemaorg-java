buildscript {
    repositories {
        mavenCentral()
    }
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'com.squins.google.fork.schema-org-client'
project.version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    api 'com.google.code.gson:gson:2.5'
    api 'com.google.guava:guava:19.0'
    implementation 'com.google.code.findbugs:jsr305:3.0.1'
    testImplementation 'junit:junit:4.+'
    testImplementation 'com.google.truth:truth:0.28'
}

java {
    toolchain {
        withJavadocJar()
        withSourcesJar()
    }
}

javadoc {
    failOnError = false
}

// Put your GPG and Sonatype credentials in "gradle.properties".
//
// Documentation for GPG:
//     https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials
if (project.hasProperty('signing.keyId') && project.hasProperty('signing.password') && project.hasProperty('signing.secretKeyRingFile') && project.hasProperty('sonatype.username') && project.hasProperty('sonatype.password')) {
    publishing {
        publications {
            Release(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version

                from components.java
            }
        }

        repositories {
            maven {
                name = "ossSonatype"
                def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots'
                def releasesRepoUrl =  'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = project.property("sonatype.username")
                    password = project.property("sonatype.password")
                }
            }
        }
    }

    signing {
        sign publishing.publications.Release
    }
}


test {
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true
}
